plugins {
    id 'com.gradleup.shadow' version '8.3.8'
}

configurations {
    shadowImplementation
    implementation.extendsFrom(shadowImplementation)
}

repositories {
    mavenCentral()
    maven {
        name = "spigotmc"
        url = uri("https://hub.spigotmc.org/nexus/content/repositories/snapshots/")
    }
    maven {
        name = "CodeMC"
        url = uri("https://repo.codemc.io/repository/maven-public/")
    }
}

archivesBaseName = "${rootProject.name}-spigot"

dependencies {
    compileOnly 'org.spigotmc:spigot-api:1.21.5-R0.1-SNAPSHOT'
    shadowImplementation "de.tr7zw:item-nbt-api:2.15.1"

    shadowImplementation(project(":core")) {
        // To avoid conflict
        exclude group: 'com.github.oshi', module: 'oshi-core'
    }

    implementation "com.github.oshi:oshi-core:6.8.2"
}

shadowJar {
    configurations = [project.configurations.shadowImplementation]

    archiveBaseName = baseName +"-build"
    archiveClassifier = ''

    relocate 'com.google.gson', 'net.opanel.deps.gson'
    relocate 'org.objectweb.asm', 'net.opanel.deps.objectweb.asm'
    relocate 'net.kyori', 'net.opanel.deps.kyori'
    relocate 'de.tr7zw.changeme.nbtapi', 'net.opanel.deps.nbtapi'

    exclude 'META-INF/*.SF'
    exclude 'META-INF/*.DSA'
    exclude 'META-INF/*.RSA'
}

build {
    dependsOn shadowJar
}
